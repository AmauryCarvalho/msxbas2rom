1 CLS
2 PRINT"  Julia Tracer - Choose your option:
3 PRINT"  ----------------------------------
4 PRINT"
5 PRINT" 1 - Cauliflower
6 PRINT" 2 - Dragon
7 PRINT" 3 - San Marco Basillica
8 PRINT" 4 - Siegel Disk
9 PRINT" 5 - Douady's Rabbit
10 PRINT" 6 - Dendrite
11 PRINT" 7 - Airplane
12 PRINT" 0 - enter your value for C
13 O$=INKEY$ : IF O$ = "" THEN GOTO 13
14 GOSUB 60
21 READCX,CY
22 IFO$="0"THEN INPUT CX : input CY
23 X1=-2.0:X2=2.0
24 XF=(X2-X1)/(256-1)
25 Y1=1.5:Y2=-1.5
26 YF=(Y2-Y1)/(192-1)
27 RR=4
28 SCREEN 5
29 FORY=0TO191
30 FORX=0TO255
31 ZY=Y1+Y*YF
32 ZX=X1+X*XF
33 C=0
34 XX=ZX*ZX:YY=ZY*ZY
35 IF ((XX+YY)>RR) OR C=255 THEN goto 40
36 ZY=2*ZX*ZY+CY
37 ZX=XX-YY+CX
38 C=C+1
39 GOTO34
40 PSET(X,Y),C
41 NEXT X
42 NEXT Y
43 GOTO43
44 DATA-0.4,0.6
45 DATA-0.8,0.156
46 DATA-0.75,0
47 DATA-0.390541,-0.586788
48 DATA-0.123,0.745
49 DATA0,1
50 DATA-1.312,0
60 IFO$="1"THENRESTORE44
61 IFO$="2"THENRESTORE45
62 IFO$="3"THENRESTORE46
63 IFO$="4"THENRESTORE47
64 IFO$="5"THENRESTORE48
65 IFO$="6"THENRESTORE49
66 IFO$="7"THENRESTORE50
67 RETURN


