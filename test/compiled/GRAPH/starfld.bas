100 REM * * * STARFIELD * * *
101 REM BY GIOVANNI NUNES, CLUBE MSX 7
105 DEFINT B-Z:KEY OFF:COLOR 15,1,1
106 SCREEN 1:WIDTH 32 : CLEAR 300
110 I=RND(-TIME)
115 S=16:A$=SPACE$(255):B=VARPTR(A$): B = PEEK(B+1)+PEEK(B+2)*255
120 FOR C=0 TO 31:J=7*RND(1)
125 IF J>3 THEN GOTO 150
130 J=J*8:I=INT(8*RND(1))
135 FOR R=BASE(5) TO BASE(5)+767 STEP 32
140 VPOKE R+C, J+I
145 I=I+1 AND 7 : NEXT R
150 NEXT C
155 L=B :FOR I=0 TO 255 STEP 64
160 K=64*RND(1):POKE L,K:L=L+1
165 FOR J=0 TO 63
170 VPOKE I+J,S*-(K=J) : NEXT J,I
175 FOR I=BASE(6) TO BASE(6)+3:READ J
180 VPOKE I,J*16:NEXT I:DATA 4,7,14,15
185 TIME=0:L=B:FOR I=0 TO 3:J=PEEK(L)
190 K=BASE(7)+64*I:VPOKE K+J,0
195 J=J+I+1 AND 63:VPOKE K+J,S
200 POKE L,J:L=L+1:NEXT I
205 IF TIME<6 THEN 205 ELSE 185

